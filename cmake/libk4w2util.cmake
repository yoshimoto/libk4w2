macro(K4W2_OPTION variable description value)
  set(__value ${value})
  set(__condition "")
  set(__varname "__value")
  foreach(arg ${ARGN})
    if(arg STREQUAL "IF" OR arg STREQUAL "if")
      set(__varname "__condition")
    else()
      list(APPEND ${__varname} ${arg})
    endif()
  endforeach()
  unset(__varname)
  if(__condition STREQUAL "")
    set(__condition 2 GREATER 1)
  endif()

  if(${__condition})
    if(__value MATCHES ";")
      if(${__value})
        option(${variable} "${description}" ON)
      else()
        option(${variable} "${description}" OFF)
      endif()
    elseif(DEFINED ${__value})
      if(${__value})
        option(${variable} "${description}" ON)
      else()
        option(${variable} "${description}" OFF)
      endif()
    else()
      option(${variable} "${description}" ${__value})
    endif()
  else()
    unset(${variable} CACHE)
  endif()
  unset(__condition)
  unset(__value)
endmacro()

function(status text)
  set(arg_condition)
  set(arg_then)
  set(arg_else)

  set(arg_current_name "condition")
  foreach(arg ${ARGN})
    if(arg STREQUAL "THEN")
      set(arg_current_name "then")
    elseif(arg STREQUAL "ELSE")
      set(arg_current_name "else")
    else()
      list(APPEND arg_${arg_current_name} ${arg})
    endif()
  endforeach()

  if(DEFINED arg_condition)
    if(DEFINED arg_then OR DEFINED arg_else)
      if(${arg_condition})
        message(STATUS "${text} ${arg_then}")
      else()
        message(STATUS "${text} ${arg_else}")
      endif()
    else()
      message(STATUS "${text} ${arg_condition}")
    endif()
  else()
    message(STATUS "${text}")
  endif()
endfunction()

